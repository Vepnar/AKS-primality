import Mathlib
import AKS.basic
-- import AKS.lemma_41

open Polynomial
variable (n p r : ‚Ñï) (hrnz : r ‚â† 0) [Fact (Nat.Prime p)] (A : ‚Ñï)

-- TODO: maybe switch a ‚â° b [MOD k] to k ‚à£ a-b (that's what we use in practice anyway).
lemma lemma42 (a b : ‚Ñï)
  (ha : a ‚àà S n p r)
  (hb : b ‚àà S n p r)
  (hab : a ‚â° b [MOD r]) :
  a ‚â° b [MOD Nat.card (G n p r hrnz)] := by

  wlog hineq : a ‚â• b
  . have fact : b ‚â• a := Nat.le_of_not_ge hineq
    exact (this n p r hrnz b a hb ha hab.symm fact).symm

  -- part one: for all polys g ‚àà ‚Ñ§/p[x][x], x^r-1 ‚à£ g(x^a) - g(x^b)
  have part1 : ‚àÄ g : (ZMod p)[X][X], AdjoinRoot.mk (f p r) (g.eval (X^a)) = AdjoinRoot.mk (f p r) (g.eval (X^b)) := by
    intro g

    let ab : (ZMod p)[X] := X^(a-b)-1
    have f_dvd_ab : f p r ‚à£ ab := by
      have : r ‚à£ a-b := (Nat.modEq_iff_dvd' hineq).mp (Nat.ModEq.symm hab)
      obtain ‚ü®k, hk‚ü© := this
      unfold ab
      rw [hk]
      have := sub_dvd_pow_sub_pow (X^r : (ZMod p)[X]) 1 k
      rw [one_pow, ‚Üê pow_mul] at this
      exact this

    let xaxb : (ZMod p)[X] := X^a - X^b
    have ab_dvd_xaxb : ab ‚à£ xaxb := by
      constructor
      rotate_left 1
      . exact X^b
      . ring_nf
        rw [‚Üê pow_add, add_comm b (a-b), Nat.sub_add_cancel hineq]
        ring

    have xaxb_dvd_gxagxb : xaxb ‚à£ g.eval (X^a) - g.eval (X^b)
      := sub_dvd_eval_sub (X^a) (X^b) g

    have : f p r ‚à£ g.eval (X^a) - g.eval (X^b)
      := dvd_trans (dvd_trans f_dvd_ab ab_dvd_xaxb) xaxb_dvd_gxagxb

    exact eq_of_sub_eq_zero (AdjoinRoot.mk_eq_zero.mpr this)

  -- part 2: applying this to elements of H
  have part2 : ‚àÄ g ‚àà H n p r, g^a = g^b := by
    intro g hg
    rw [ha, hb] <;> try assumption

    have : Œ± p r ^ a = Œ± p r ^ b := calc
      _ = AdjoinRoot.mk (f p r) (X^a) := by rfl
      _ = AdjoinRoot.mk (f p r) ((X : (ZMod p)[X][X]).eval (X^a)) := by rw [eval_X]
      _ = AdjoinRoot.mk (f p r) ((X : (ZMod p)[X][X]).eval (X^b)) := part1 X
      _ = AdjoinRoot.mk (f p r) (X^b) := by rw[eval_X (x := X^b)]
      _ = _ := by rfl

    simp only [this]

  have : ‚àÄ g ‚àà H n p r, (œÜ p r hrnz g)^a = (œÜ p r hrnz g)^b
    := Œª g hg ‚Ü¶ calc
    _ = œÜ p r hrnz (g^a) := by rw [map_pow]
    _ = œÜ p r hrnz (g^b) := by rw[part2]; exact hg
    _ = (œÜ p r hrnz g)^b := by rw [map_pow]

  -- part 3: applying this to elements of G
  have : ‚àÄ g ‚àà G n p r hrnz, g^a = g^b := Œª g ‚ü®q, hq, hqg‚ü© ‚Ü¶ by
    have := this q hq
    have := calc
      (rfl.mp (‚Üë g : ùîΩ p r))^a = (œÜ p r hrnz q)^a := by rw[‚Üê hqg]; rfl
      _ = (œÜ p r hrnz q)^b := this
      _ = (rfl.mp (‚Üë g : ùîΩ p r))^b := by rw[‚Üê hqg]; rfl

    exact Units.eq_iff.mp this

  have : ‚àÄ g ‚àà G n p r hrnz, g^(a-b) = 1 := by
    intro g hg
    have : g^(a-b) * g^b = 1 * g^b := by
      rw [pow_sub_mul_pow (h := hineq), one_mul, this g hg]
    exact mul_right_cancel this

  -- part 4: concluding that #G divides a-b
  -- switching to g : ‚Ü• G instead of g ‚àà G because that's what isCyclic_iff_exists_orderOf_eq_natCard gives you
  -- maybe should do that everywhere for consistency's sake
  have order_divides_ab : ‚àÄ (g : ‚Ü• (G n p r hrnz)), orderOf g ‚à£ a-b := by --substituting names for variables, here for a-b?
    intro g
    rw[orderOf_dvd_iff_pow_eq_one]
    have := this (‚Üë g) (g.property)
    exact SetLike.coe_eq_coe.mp this

  have : ‚àÉ (g : ‚Ü•(G n p r hrnz)), orderOf g = Nat.card (G n p r hrnz) := isCyclic_iff_exists_orderOf_eq_natCard.mp inferInstance

  obtain ‚ü®g, hg‚ü© := this

  have : orderOf g ‚à£ a-b
    := order_divides_ab g

  rw [‚Üê hg]
  symm
  exact (Nat.modEq_iff_dvd' hineq).mpr this
